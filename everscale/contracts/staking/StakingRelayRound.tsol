pragma ever-solidity >= 0.62.0;


import "./interfaces/IStakingRelayRound.tsol";
import "./interfaces/IStakingPool.tsol";
import "./interfaces/IUserData.tsol";

import "./libraries/Gas.tsol";
import "./libraries/PlatformTypes.tsol";
import "./../utils/ErrorCodes.tsol";

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "@broxus/contracts/contracts/platform/Platform.tsol";


contract StakingRelayRound is IStakingRelayRound {
    event RelayRoundCodeUpgraded(uint32 code_version);

    bool relays_installed;
    uint32 public relays_count;
    uint32 public start_time;
    uint32 public end_time;
    uint128 public total_tokens_staked;
    uint32 public reward_round_num;
    uint128 public round_reward;
    bool public duplicate;
    uint8 public expected_packs_num;
    address election_addr;
    address prev_round_addr;

    uint32 round_num; // setup from initialData
    uint256[] ton_keys; // array of ton pubkeys
    uint160[] eth_addrs; // array of eth pubkeys
    address[] staker_addrs; // array of staker addrs
    uint128[] staked_tokens; // array of staked tokens

    mapping (address => uint256) addr_to_idx;
    mapping (address => bool) reward_claimed;

    uint8 relay_packs_installed;

    // user when sending relays to new relay round
    uint256 relay_transfer_start_idx = 0;

    uint32 current_version;
    TvmCell platform_code;

    address root; // setup from initialData

    uint256 constant SCALING_FACTOR = 1e18;
    uint32 constant EXT_CALL_INTERVAL = 12 hours;

    // Cant be deployed directly
    constructor() { revert(); }

    function getDetails() external view override responsible returns (RelayRoundDetails) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS }RelayRoundDetails(
            root, round_num, ton_keys, eth_addrs, staker_addrs, staked_tokens, relays_installed, current_version
        );
    }

    function hasUnclaimedReward(address _relay_staker_addr) external view responsible returns (bool has_reward) {
        if (!addr_to_idx.exists(_relay_staker_addr)) {
            return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} false;
        }
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} !reward_claimed[_relay_staker_addr];
    }

    function getRelayByStakerAddress(
        address _relay_staker_addr
    ) external view responsible returns (uint256 _ton_key, uint160 _eth_addr, address _staker_addr, uint128 _staked_tokens) {
        require (addr_to_idx.exists(_relay_staker_addr), ErrorCodes.RELAY_NOT_EXIST);
        uint256 idx = addr_to_idx[_relay_staker_addr];
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} (ton_keys[idx], eth_addrs[idx], staker_addrs[idx], staked_tokens[idx]);
    }

    function getRewardForRound(address staker_addr, uint32 code_version) external override onlyUserData(staker_addr) {
        require (block.timestamp >= end_time, ErrorCodes.RELAY_ROUND_NOT_ENDED);
        require (reward_claimed[staker_addr] == false, ErrorCodes.RELAY_REWARD_CLAIMED);
        require (code_version == current_version, ErrorCodes.LOW_VERSION);
        require (addr_to_idx.exists(staker_addr), ErrorCodes.RELAY_NOT_EXIST);

        tvm.rawReserve(Gas.RELAY_ROUND_INITIAL_BALANCE, 0);

        reward_claimed[staker_addr] = true;
        uint256 staker_reward_share = (staked_tokens[addr_to_idx[staker_addr]] * SCALING_FACTOR) / total_tokens_staked;
        uint128 relay_reward = uint128((staker_reward_share * round_reward) / SCALING_FACTOR);

        IUserData(msg.sender).receiveRewardForRelayRound{ value: 0, flag: MsgFlag.ALL_NOT_RESERVED }(round_num, reward_round_num, relay_reward);
    }

    function _getRelayListFromIdx(
        uint128 limit, uint256 start_idx
    ) internal view returns (
        uint256[] _ton_keys,
        uint160[] _eth_addrs,
        address[] _staker_addrs,
        uint128[] _staked_tokens,
        uint256 new_idx
    ) {
        uint256[] _ton_keys_limit = new uint256[](limit);
        uint160[] _eth_addrs_limit = new uint160[](limit);
        address[] _staker_addrs_limit = new address[](limit);
        uint128[] _staked_tokens_limit = new uint128[](limit);

        uint256 cur_idx = start_idx;
        uint128 counter = 0;

        while (counter < limit && cur_idx < ton_keys.length) {
            _ton_keys_limit[counter] = ton_keys[cur_idx];
            _eth_addrs_limit[counter] = eth_addrs[cur_idx];
            _staker_addrs_limit[counter] = staker_addrs[cur_idx];
            _staked_tokens_limit[counter] = staked_tokens[cur_idx];
            counter++;
            cur_idx++;
        }
        return (_ton_keys_limit, _eth_addrs_limit, _staker_addrs_limit, _staked_tokens_limit, cur_idx);
    }

    function sendRelaysToRelayRound(address relay_round_addr, uint32 count) external override onlyRoot {
        tvm.rawReserve(Gas.RELAY_ROUND_INITIAL_BALANCE, 0);

        if (relay_transfer_start_idx >= ton_keys.length) {
            IStakingRelayRound(relay_round_addr).setEmptyRelays{ value: 0, flag: MsgFlag.ALL_NOT_RESERVED }();
            return;
        }

        (
            uint256[] _ton_keys,
            uint160[] _eth_addrs,
            address[] _staker_addrs,
            uint128[] _staked_tokens,
            uint256 _new_last_idx
        ) = _getRelayListFromIdx(count, relay_transfer_start_idx);
        relay_transfer_start_idx = _new_last_idx;

        IStakingRelayRound(relay_round_addr)
            .setRelays{ value: 0, flag: MsgFlag.ALL_NOT_RESERVED }
            (_ton_keys, _eth_addrs, _staker_addrs, _staked_tokens);
    }

    function relayKeys() public view responsible returns (uint256[]) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } ton_keys;
    }

    function setEmptyRelays() external override {
        require (msg.sender == election_addr || msg.sender == prev_round_addr, ErrorCodes.BAD_SENDER);
        require (!relays_installed, ErrorCodes.RELAY_ROUND_INITIALIZED);

        tvm.rawReserve(Gas.RELAY_ROUND_INITIAL_BALANCE, 0);

        relay_packs_installed += 1;
        _checkRelaysInstalled();
    }

    function setRelays(
        uint256[] _ton_keys,
        uint160[] _eth_addrs,
        address[] _staker_addrs,
        uint128[] _staked_tokens
    ) external override {
        require (msg.sender == election_addr || msg.sender == prev_round_addr || msg.sender == root, ErrorCodes.BAD_SENDER);
        require (!relays_installed, ErrorCodes.RELAY_ROUND_INITIALIZED);

        tvm.rawReserve(Gas.RELAY_ROUND_INITIAL_BALANCE, 0);

        for (uint i = 0; i < _ton_keys.length; i++) {
            if (_staked_tokens[i] > 0) {
                ton_keys.push(_ton_keys[i]);
                eth_addrs.push(_eth_addrs[i]);
                staked_tokens.push(_staked_tokens[i]);
                staker_addrs.push(_staker_addrs[i]);

                addr_to_idx[_staker_addrs[i]] = staker_addrs.length - 1;
                total_tokens_staked += _staked_tokens[i];
                relays_count += 1;
            } else {
                break;
            }
        }

        relay_packs_installed += 1;
        _checkRelaysInstalled();
    }

    function _checkRelaysInstalled() internal {
        // tvm.rawReserve should be called before calling this!
        if (relay_packs_installed == expected_packs_num) {
            relays_installed = true;

            IStakingPool(root).onRelayRoundInitialized{ value: 0, flag: MsgFlag.ALL_NOT_RESERVED }(
                round_num, start_time, end_time, relays_count, round_reward, reward_round_num, duplicate, eth_addrs, ton_keys
            );
            return;
        }
        root.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
    }


    // should be called root after +2 rounds!
    function destroy() external override onlyRoot {
        // small safety check
        require (block.timestamp >= end_time, ErrorCodes.RELAY_ROUND_NOT_ENDED);

        root.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
    }

    function onCodeUpgrade(TvmCell upgrade_data) private {
        TvmSlice s = upgrade_data.toSlice();
        (address _root,,) = s.load(address, uint8, address);

        TvmCell _platform_code = s.loadRef();

        TvmSlice initialData = s.loadRefAsSlice();
        uint32 _round_num = initialData.load(uint32);

        TvmSlice params = s.loadRefAsSlice();
        (uint32 _current_version, uint32 _prev_version) = params.load(uint32, uint32);
        // DEPLOY
        if (_current_version == _prev_version) {
            tvm.resetStorage();
            tvm.rawReserve(Gas.RELAY_ROUND_INITIAL_BALANCE, 0);

            root = _root;
            platform_code = _platform_code;
            round_num = _round_num;
            current_version = _current_version;

            start_time = params.load(uint32);
            end_time = params.load(uint32);

            reward_round_num = params.load(uint32);
            round_reward = params.load(uint128);
            duplicate = params.load(bool);
            expected_packs_num = params.load(uint8);
            election_addr = params.load(address);
            prev_round_addr = params.load(address);

            IStakingPool(root).onRelayRoundDeployed{ value: 0, flag: MsgFlag.ALL_NOT_RESERVED }(round_num, duplicate);
        } else {
            // UPGRADE from broken version
            tvm.rawReserve(Gas.RELAY_ROUND_INITIAL_BALANCE, 0);

            IStakingPool(root).onRelayRoundInitialized{ value: 0, flag: MsgFlag.ALL_NOT_RESERVED }(
                round_num, start_time, end_time, relays_count, round_reward, reward_round_num, duplicate, eth_addrs, ton_keys
            );
        }
    }

    function upgrade(TvmCell code, uint32 new_version, address send_gas_to) external onlyRoot {
        if (new_version == current_version) {
            tvm.rawReserve(Gas.RELAY_ROUND_INITIAL_BALANCE, 0);
            send_gas_to.transfer({ value: 0, bounce: false, flag: MsgFlag.ALL_NOT_RESERVED });
        } else {
            emit RelayRoundCodeUpgraded(new_version);

            TvmBuilder builder;
            uint8 _tmp;
            builder.store(root); // address 267
            builder.store(_tmp); // 8
            builder.store(send_gas_to); // address 267

            builder.store(platform_code); // ref1

            TvmBuilder initial;
            initial.store(round_num); // 32

            builder.storeRef(initial); // ref2

            TvmBuilder params;
            params.store(new_version); // 32
            params.store(current_version); // 32

            builder.storeRef(params); // ref3

            TvmBuilder data_builder;

            TvmBuilder data_builder_1;
            data_builder_1.store(relays_installed); // 1
            data_builder_1.store(relays_count); // 32
            data_builder_1.store(start_time); // 32
            data_builder_1.store(end_time); // 32
            data_builder_1.store(total_tokens_staked); // 128
            data_builder_1.store(reward_round_num); // 32
            data_builder_1.store(round_reward); // 128
            data_builder_1.store(duplicate); // 1
            data_builder_1.store(expected_packs_num); // 8
            data_builder_1.store(eth_addrs); // 33 + ref1
            data_builder_1.store(staker_addrs); // 33 + ref2
            // TOTAL 460

            data_builder.storeRef(data_builder_1);

            TvmBuilder data_builder_2;
            data_builder_2.store(election_addr); // address 267
            data_builder_2.store(prev_round_addr); // address 267
            data_builder_2.store(staked_tokens); // 33 + ref1
            data_builder_2.store(ton_keys); // 33 + ref2
            data_builder_2.store(addr_to_idx); // 33 + ref3
            data_builder_2.store(reward_claimed); // 33 + ref4
            data_builder_2.store(relay_packs_installed); // 8
            data_builder_2.store(relay_transfer_start_idx); // 256
            // TOTAL 930

            data_builder.storeRef(data_builder_2);

            builder.storeRef(data_builder); // ref4

            // set code after complete this method
            tvm.setcode(code);

            // run onCodeUpgrade from new code
            tvm.setCurrentCode(code);
            onCodeUpgrade(builder.toCell());
        }
    }

    /*
    upgrade_data
        bits:
            address root
            uint8 dummy
            address send_gas_to
        refs:
            1: platform_code
            2: initial
                bits:
                    uint128 round_num
            3: params:
                bits:
                    uint32 new_version
                    uint32 current_version
            4: data
                refs:
                    1: data_1
                        bits:
                            bool relays_installed
                            uint32 relays_count
                            uint32 start_time
                            uint32 end_time
                            uint128 total_tokens_staked
                            uint32 reward_round_num
                            uint128 round_reward
                            bool duplicate
                            uint8 expected_packs_num
                        refs:
                            1: eth_addrs
                            2: staker_addrs
                    2: data_2
                        bits:
                            address election_addr
                            address prev_round_addr
                            uint8 relay_packs_installed
                            uint256 relay_transfer_start_idx
                        refs:
                            1: staked_tokens
                            2: ton_keys
                            3: addr_to_idx
                            4: reward_claimed
    */

    function _buildInitData(uint8 type_id, TvmCell _initialData) internal view returns (TvmCell) {
        return tvm.buildStateInit({
            contr: Platform,
            varInit: {
                root: root,
                platformType: type_id,
                initialData: _initialData,
                platformCode: platform_code
            },
            pubkey: 0,
            code: platform_code
        });
    }

    function _buildUserDataParams(address user) private pure returns (TvmCell) {
        TvmBuilder builder;
        builder.store(user);
        return builder.toCell();
    }

    function getUserDataAddress(address user) public view responsible returns (address) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } address(tvm.hash(_buildInitData(
            PlatformTypes.UserData,
            _buildUserDataParams(user)
        )));
    }

    modifier onlyUserData(address user) {
        address expectedAddr = getUserDataAddress(user);
        require (expectedAddr == msg.sender, ErrorCodes.NOT_USER_DATA);
        _;
    }

    modifier onlyRoot() {
        require(msg.sender == root, ErrorCodes.NOT_ROOT);
        _;
    }

}
