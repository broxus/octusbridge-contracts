pragma ever-solidity >= 0.62.0;


import "@broxus/contracts/contracts/utils/RandomNonce.tsol";
import "@broxus/contracts/contracts/utils/CheckPubKey.tsol";

import "./../../utils/TransferUtils.tsol";
import "./../event-configuration-contracts/solana/EverscaleSolanaEventConfiguration.tsol";
import "./../interfaces/event-configuration-contracts/IEverscaleSolanaEventConfiguration.tsol";


contract EverscaleSolanaEventConfigurationFactory is TransferUtils, RandomNonce, CheckPubKey {
    TvmCell public configurationCode;

    constructor(TvmCell _configurationCode) public checkPubKey {
        tvm.accept();

        configurationCode = _configurationCode;
    }

    function deploy(
        address _owner,
        IEverscaleSolanaEventConfiguration.BasicConfiguration basicConfiguration,
        IEverscaleSolanaEventConfiguration.EverscaleSolanaEventConfiguration networkConfiguration
    ) external view reserveMinBalance(Gas.CONFIGURATION_FACTORY_INITIAL_BALANCE) {
        TvmCell _meta;

        new EverscaleSolanaEventConfiguration{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED,
            code: configurationCode,
            pubkey: 0,
            varInit: {
                basicConfiguration: basicConfiguration,
                networkConfiguration: networkConfiguration
            }
        }(_owner, _meta);
    }

    function deriveConfigurationAddress(
        IEverscaleSolanaEventConfiguration.BasicConfiguration basicConfiguration,
        IEverscaleSolanaEventConfiguration.EverscaleSolanaEventConfiguration networkConfiguration
    ) external view returns(address) {
        TvmCell stateInit = tvm.buildStateInit({
            contr: EverscaleSolanaEventConfiguration,
            varInit: {
                basicConfiguration: basicConfiguration,
                networkConfiguration: networkConfiguration
            },
            pubkey: 0,
            code: configurationCode
        });

        return address(tvm.hash(stateInit));
    }
}
