pragma ever-solidity >= 0.62.0;

pragma AbiHeader pubkey;
pragma AbiHeader expire;

import "@broxus/contracts/contracts/access/InternalOwner.tsol";
import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "./../../utils/TransferUtils.tsol";
import "../interfaces/alien-token-merge/IMergeRouter.tsol";
import "../../utils/ErrorCodes.tsol";

contract MergeRouter is IMergeRouter, InternalOwner, TransferUtils {
    address static proxy;
    address static token;

    address pool;
    address public manager;

    modifier onlyOwnerOrManager() {
        require(msg.sender == owner || msg.sender == manager, ErrorCodes.ONLY_OWNER_OR_MANAGER);

        _;
    }

    constructor(
        address _owner,
        address _manager
    ) {
        require(msg.sender == proxy, ErrorCodes.WRONG_PROXY);

        manager = _manager;

        setOwnership(_owner);
    }

    /// @notice Set pool address. Cant be zero address.
    /// Can be called only by `owner` or `manager`
    /// @param pool_ Pool address
    function setPool(
        address pool_
    ) external override cashBack onlyOwnerOrManager {
        require(pool_.value != 0, ErrorCodes.MERGE_POOL_IS_ZERO_ADDRESS);

        pool = pool_;
    }

    function setManager(
        address _manager
    ) external override cashBack onlyOwner {
        manager = _manager;
    }

    /// @notice Set pool address to zero address
    /// Can be called only by `owner` or `manager`
    function disablePool() external override cashBack onlyOwnerOrManager {
        pool = address.makeAddrStd(0, 0);
    }

    /// @notice Get pool address
    /// @return Pool address. Zero address if no pool was set.
    function getPool() external override responsible returns (address) {
        return{value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS} pool;
    }

    /// @notice Get router details
    function getDetails() external override responsible returns (
        address _proxy,
        address _token,
        address _pool
    ) {
        return{value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS} (proxy, token, pool);
    }
}
