pragma ton-solidity >= 0.39.0;
pragma AbiHeader expire;


import "./../interfaces/event-contracts/IEthereumEverscaleEvent.tsol";
import "./../interfaces/event-configuration-contracts/IEthereumEverscaleEventConfiguration.tsol";

import "./../interfaces/IEthereumEverscaleProxy.tsol";
import "./../interfaces/IEthereumEverscaleProxyExtended.tsol";

import "./../event-contracts/base/EthereumEverscaleBaseEvent.tsol";

import "./../../utils/TransferUtils.tsol";
import "./../../utils/ErrorCodes.tsol";

import "@broxus/contracts/contracts/access/InternalOwner.tsol";
import "@broxus/contracts/contracts/utils/CheckPubKey.tsol";
import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";


/// @title Basic Ethereum-Everscale event configuration contract.
/// @author https://github.com/broxus
contract EthereumEverscaleEventConfiguration is
    IEthereumEverscaleEventConfiguration,
    IEthereumEverscaleProxy,
    IEthereumEverscaleProxyExtended,
    TransferUtils,
    InternalOwner,
    CheckPubKey
{
    BasicConfiguration public static basicConfiguration;
    EthereumEverscaleEventConfiguration public static networkConfiguration;

    uint64 public flags;
    TvmCell public meta;

    /// @param _owner Event configuration owner
    constructor(
        address _owner,
        TvmCell _meta
    ) public checkPubKey {
        tvm.accept();

        _reserveTargetBalance();

        setOwnership(_owner);

        meta = _meta;

        _owner.transfer({
            value: 0,
            bounce: false,
            flag: MsgFlag.ALL_NOT_RESERVED
        });
    }

    /// @notice Set new configuration meta.
    /// @param _meta New configuration meta
    function setMeta(TvmCell _meta) override onlyOwner cashBack external {
        meta = _meta;
    }

    /// @dev Set end block number. Can be set only in case current value is 0.
    /// @param endBlockNumber End block number
    function setEndBlockNumber(
        uint32 endBlockNumber
    )
        override
        onlyOwner
        external
    {
        require(
            networkConfiguration.endBlockNumber == 0,
            ErrorCodes.END_BLOCK_NUMBER_ALREADY_SET
        );

        require(
            endBlockNumber >= networkConfiguration.startBlockNumber,
            ErrorCodes.TOO_LOW_END_BLOCK_NUMBER
        );

        networkConfiguration.endBlockNumber = endBlockNumber;
    }

    function setFlags(
        uint64 _flags
    ) override onlyOwner cashBack external {
        flags = _flags;
    }

    function setEventInitialBalance(
        uint64 eventInitialBalance
    ) override onlyOwner cashBack external {
        basicConfiguration.eventInitialBalance = eventInitialBalance;
    }


    /// @dev Build initial data for event contract
    /// @dev Extends event vote data with configuration params
    /// @param eventVoteData Event vote data structure, passed by relay
    function buildEventInitData(
        IEthereumEverscaleEvent.EthereumEverscaleEventVoteData eventVoteData
    ) internal view returns(
        IEthereumEverscaleEvent.EthereumEverscaleEventInitData eventInitData
    ) {
        eventInitData.voteData = eventVoteData;

        eventInitData.configuration = address(this);
        eventInitData.staking = basicConfiguration.staking;
        eventInitData.chainId = networkConfiguration.chainId;
    }

    /// @dev Deploy event contract
    /// @param eventVoteData Event vote data
    function deployEvent(
        IEthereumEverscaleEvent.EthereumEverscaleEventVoteData eventVoteData
    )
        external
        override
        reserveAtLeastTargetBalance()
    {
        _deployEvent(eventVoteData, MsgFlag.ALL_NOT_RESERVED, 0);
    }

    function deployEvents(
        IEthereumEverscaleEvent.EthereumEverscaleEventVoteData[] eventsVoteData,
        uint128[] values
    ) external override reserveAtLeastTargetBalance() {
        require(values.length == eventsVoteData.length && values.length > 0);

        uint counter = 0;

        for (IEthereumEverscaleEvent.EthereumEverscaleEventVoteData eventVoteData: eventsVoteData) {
            _deployEvent(eventVoteData, 0, values[counter]);
            counter++;
        }

        msg.sender.transfer({ value: 0, flag: MsgFlag.ALL_NOT_RESERVED });
    }


    function _deployEvent(
        IEthereumEverscaleEvent.EthereumEverscaleEventVoteData eventVoteData,
        uint8 flag,
        uint128 value
    ) internal view {
        require(msg.value >= basicConfiguration.eventInitialBalance, ErrorCodes.TOO_LOW_DEPLOY_VALUE);
        require(
            eventVoteData.eventBlockNumber >= networkConfiguration.startBlockNumber,
            ErrorCodes.EVENT_BLOCK_NUMBER_LESS_THAN_START
        );

        if (networkConfiguration.endBlockNumber != 0) {
            require(
                eventVoteData.eventBlockNumber <= networkConfiguration.endBlockNumber,
                ErrorCodes.EVENT_BLOCK_NUMBER_HIGHER_THAN_END
            );
        }

        IEthereumEverscaleEvent.EthereumEverscaleEventInitData eventInitData = buildEventInitData(eventVoteData);

        address eventContract = deriveEventAddress(eventVoteData);

        emit NewEventContract(eventContract);

        new EthereumEverscaleBaseEvent{
            value: value,
            flag: flag,
            code: basicConfiguration.eventCode,
            pubkey: 0,
            varInit: {
                eventInitData: eventInitData
            }
        }(msg.sender, meta);
    }

    /// @dev Derive the Ethereum event contract address from it's init data
    /// @param eventVoteData Ethereum event vote data
    /// @return eventContract Address of the corresponding ethereum event contract
    function deriveEventAddress(
        IEthereumEverscaleEvent.EthereumEverscaleEventVoteData eventVoteData
    )
        override
        public
        view
        responsible
    returns(
        address eventContract
    ) {
        IEthereumEverscaleEvent.EthereumEverscaleEventInitData eventInitData = buildEventInitData(eventVoteData);

        TvmCell stateInit = tvm.buildStateInit({
            contr: EthereumEverscaleBaseEvent,
            varInit: {
                eventInitData: eventInitData
            },
            pubkey: 0,
            code: basicConfiguration.eventCode
        });

        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} address(tvm.hash(stateInit));
    }

    /**
        @dev Get configuration details.
        @return _basicConfiguration Basic configuration init data
        @return _networkConfiguration Network specific configuration init data
    */
    function getDetails() override public view responsible returns(
        BasicConfiguration _basicConfiguration,
        EthereumEverscaleEventConfiguration _networkConfiguration,
        TvmCell _meta
    ) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false}(
            basicConfiguration,
            networkConfiguration,
            meta
        );
    }

    /// @dev Get event configuration type
    /// @return _type Configuration type - Ethereum or Everscale
    function getType() override public pure responsible returns(EventType _type) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} EventType.EthereumEverscale;
    }

    function getFlags() override public view responsible returns(uint64 _flags) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} flags;
    }

    /// @dev Proxy V1 callback.
    /// Receives "confirm" callback from the event contract and checks event contract correctness.
    /// If it's correct, then sends the callback to the proxy with the same signature.
    /// @param eventInitData Ethereum event data
    /// @param gasBackAddress Gas back address
    function onEventConfirmed(
        IEthereumEverscaleEvent.EthereumEverscaleEventInitData eventInitData,
        address gasBackAddress
    ) external override reserveAtLeastTargetBalance() {
        require(
            eventInitData.configuration == address(this),
            ErrorCodes.SENDER_NOT_EVENT_CONTRACT
        );

        address eventContract = _deriveEventAddress(eventInitData);

        require(
            eventContract == msg.sender,
            ErrorCodes.SENDER_NOT_EVENT_CONTRACT
        );

        IEthereumEverscaleProxy(networkConfiguration.proxy).onEventConfirmed{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(eventInitData, gasBackAddress);

    }

    /// @dev Proxy V2 callback. Receives additional cell.
    /// Receives "confirm" callback from the event contract and checks event contract correctness.
    /// If it's correct, then sends the callback to the proxy with the same signature.
    /// @param eventInitData Ethereum event data
    /// @param _meta Arbitrary meta cell
    /// @param gasBackAddress Gas back address
    function onEventConfirmedExtended(
        IEthereumEverscaleEvent.EthereumEverscaleEventInitData eventInitData,
        TvmCell _meta,
        address gasBackAddress
    ) external override reserveAtLeastTargetBalance {
        require(
            eventInitData.configuration == address(this),
            ErrorCodes.SENDER_NOT_EVENT_CONTRACT
        );

        address eventContract = _deriveEventAddress(eventInitData);

        require(
            eventContract == msg.sender,
            ErrorCodes.SENDER_NOT_EVENT_CONTRACT
        );

        IEthereumEverscaleProxyExtended(networkConfiguration.proxy).onEventConfirmedExtended{
            flag: MsgFlag.ALL_NOT_RESERVED
        }(eventInitData, _meta, gasBackAddress);
    }

    function _deriveEventAddress(
        IEthereumEverscaleEvent.EthereumEverscaleEventInitData eventInitData
    ) internal view returns (address _event) {
        TvmCell stateInit = tvm.buildStateInit({
            contr: EthereumEverscaleBaseEvent,
            varInit: {
                eventInitData: eventInitData
            },
            pubkey: 0,
            code: basicConfiguration.eventCode
        });

        _event = address(tvm.hash(stateInit));
    }
}
