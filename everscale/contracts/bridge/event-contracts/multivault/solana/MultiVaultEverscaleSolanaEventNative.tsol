pragma ever-solidity >= 0.62.0;

pragma AbiHeader pubkey;
pragma AbiHeader expire;

import "ton-eth-bridge-token-contracts/contracts/interfaces/ITokenRoot.tsol";
import "ton-eth-bridge-token-contracts/contracts/interfaces/TIP3TokenRoot.tsol";

import "./../../base/solana/EverscaleSolanaBaseEvent.tsol";
import "./../../../interfaces/event-contracts/multivault/solana/IMultiVaultEverscaleSolanaEventNative.tsol";


/// @notice Everscale-Solana event for MultiVault native token transfer.
/// Before switching into the `Pending` status, event contract must perform
/// the following actions:
/// - Verify that the `tokenWallet` is a correct token wallet for `token`, owned by the proxy
/// - Obtain the `token` metadata (name, symbol, decimals)
/// - Rewrite the `eventData` with the correct value
contract MultiVaultEverscaleSolanaEventNative is
    EverscaleSolanaBaseEvent,
    IMultiVaultEverscaleSolanaEventNative
{
    uint32 public nonce;
    address public proxy;
    address public tokenWallet;
    address public token;
    address public remainingGasTo;
    uint128 public amount;
    uint256 public recipient;

    address public sender;
    uint128 public initial_balance;

    string public name;
    string public symbol;
    uint8 public decimals;
    address public expectedTokenWallet;
    bytes public solanaPayload;

    constructor(
        address _initializer,
        TvmCell _meta
    ) EverscaleSolanaBaseEvent(_initializer, _meta) {}

    function afterSignatureCheck(TvmSlice body, TvmCell /*message*/) private inline view returns (TvmSlice) {
        body.load(uint64, uint32);
        TvmSlice bodyCopy = body;
        uint32 functionId = body.load(uint32);
        if (isExternalVoteCall(functionId)){
            require(votes[msg.pubkey()] == Vote.Empty, ErrorCodes.KEY_VOTE_NOT_EMPTY);
        }
        return bodyCopy;
    }

    function onInit() override internal {
        (
            nonce,
            proxy,
            tokenWallet,
            token,
            remainingGasTo,
            amount,
            recipient,
            sender,
            solanaPayload
        ) = abi.decode(
            eventInitData.voteData.eventData,
            (uint32, address, address, address, address, uint128, uint256, address, bytes)
        );

        initial_balance = address(this).balance;

        uint128 value = gasToValue(Gas.GET_TOKEN_DATA_GAS, address(this).wid);

        ITokenRoot(token).name{
            value: value,
            bounce: true,
            callback: MultiVaultEverscaleSolanaEventNative.receiveTokenName
        }();

        ITokenRoot(token).symbol{
            value: value,
            bounce: true,
            callback: MultiVaultEverscaleSolanaEventNative.receiveTokenSymbol
        }();

        ITokenRoot(token).decimals{
            value: value,
            bounce: true,
            callback: MultiVaultEverscaleSolanaEventNative.receiveTokenDecimals
        }();

        ITokenRoot(token).walletOf{
            value: value,
            bounce: true,
            callback: MultiVaultEverscaleSolanaEventNative.receiveProxyTokenWallet
        }(proxy);
    }

    function receiveTokenName(
        string name_
    ) external override {
        require(msg.sender == token, ErrorCodes.SENDER_NOT_ROOT);

        name = name_;
    }

    function receiveTokenSymbol(
        string symbol_
    ) external override {
        require(msg.sender == token, ErrorCodes.SENDER_NOT_ROOT);

        symbol = symbol_;
    }

    function receiveTokenDecimals(
        uint8 decimals_
    ) external override {
        require(msg.sender == token, ErrorCodes.SENDER_NOT_ROOT);

        decimals = decimals_;
    }

    function receiveProxyTokenWallet(
        address tokenWallet_
    ) external override {
        require(msg.sender == token, ErrorCodes.SENDER_NOT_ROOT);

        expectedTokenWallet = tokenWallet_;

        if (tokenWallet == expectedTokenWallet) {
            _updateEventData();

            loadRelays();
        } else {
            setStatusRejected(1);
        }
    }

    function getDecodedData() external override responsible returns(
        address proxy_,
        address tokenWallet_,
        address token_,
        address remainingGasTo_,
        uint128 amount_,
        uint256 recipient_,
        string name_,
        string symbol_,
        uint8 decimals_,
        bytes solanaPayload_
    ) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false}(
            proxy,
            tokenWallet,
            token,
            remainingGasTo,
            amount,
            recipient,
            name,
            symbol,
            decimals,
            solanaPayload
        );
    }

    function _updateEventData() internal {
        eventInitData.voteData.eventData = abi.encode(
            token,

            name,
            symbol,
            decimals,

            amount,
            recipient,
            solanaPayload
        );
    }

    function gasBackAddress() internal override view returns(address) {
        return remainingGasTo;
    }

    onBounce(TvmSlice slice) external {
        uint32 selector = slice.load(uint32);

        if (
            (selector == tvm.functionId(TIP3TokenRoot.name) && msg.sender == token )||
            (selector == tvm.functionId(TIP3TokenRoot.symbol) && msg.sender == token) ||
            (selector == tvm.functionId(TIP3TokenRoot.decimals) && msg.sender == token) ||
            (selector == tvm.functionId(ITokenRoot.walletOf) && msg.sender == token)
        ) {
            setStatusRejected(2);
        }
    }
}
