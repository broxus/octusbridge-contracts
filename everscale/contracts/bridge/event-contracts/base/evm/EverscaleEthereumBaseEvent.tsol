pragma ever-solidity >= 0.39.0;
pragma AbiHeader time;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "./../BaseEvent.tsol";
import "../../../interfaces/event-contracts/IEverscaleEthereumEvent.tsol";

import "./../../../../utils/ErrorCodes.tsol";


contract EverscaleEthereumBaseEvent is BaseEvent, IEverscaleEthereumEvent {
    uint32 constant FORCE_CLOSE_TIMEOUT = 1 days;
    uint32 public createdAt;

    // Event data
    EverscaleEthereumEventInitData static eventInitData;
    // Ethereum payload signatures for confirmations
    mapping (uint => bytes) public signatures;

    event Confirm(uint relay, bytes signature);

    /*
        @dev Should be deployed only by EverscaleEthereumEventConfiguration contract
        @param _initializer The address who paid for contract deployment.
        Receives all contract balance at the end of event contract lifecycle.
    */
    constructor(
        address _initializer,
        TvmCell _meta
    ) public {
        require(
            eventInitData.configuration == msg.sender,
            ErrorCodes.SENDER_NOT_EVENT_CONFIGURATION
        );

        createdAt = now;
        initializer = _initializer;
        meta = _meta;

        onInit();
    }

    function getEventInitData() public view responsible returns (EverscaleEthereumEventInitData) {
        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} eventInitData;
    }

    function getStakingAddress() override internal view returns (address) {
        return eventInitData.staking;
    }

    function isExternalVoteCall(uint32 functionId) override internal view returns (bool){
        return functionId == tvm.functionId(confirm) || functionId == tvm.functionId(reject);
    }

    /*
        @dev Confirm event
        @dev Can be called only when event configuration is in Pending status
        @param eventDataSignature Relay's signature of the TonEvent data
        @param voteReceiver Should be always equal to the event contract address
    */
    function confirm(bytes signature, address voteReceiver) public {
        _checkVoteReceiver(voteReceiver);

        uint relay = msg.pubkey();

        require(votes[relay] == Vote.Empty, ErrorCodes.KEY_VOTE_NOT_EMPTY);

        tvm.accept();

        votes[relay] = Vote.Confirm;
        signatures[relay] = signature;

        emit Confirm(relay, signature);
        confirms++;

        // Event already confirmed
        if (status() != Status.Pending) {
            return;
        }

        if (confirms >= requiredVotes) {
            setStatusConfirmed();

            onConfirm();
        }
    }

    /*
        @dev Reject event
        @dev Can be called only when event configuration is in Pending status
        @param voteReceiver Should be always equal to the event contract address
    */
    function reject(address voteReceiver) public {
        _checkVoteReceiver(voteReceiver);

        uint relay = msg.pubkey();

        require(votes[relay] == Vote.Empty, ErrorCodes.KEY_VOTE_NOT_EMPTY);

        tvm.accept();

        votes[relay] = Vote.Reject;

        emit Reject(relay);
        rejects++;

        // Event already confirmed
        if (status() != Status.Pending) {
            return;
        }

        if (rejects >= requiredVotes) {
            setStatusRejected(0);

            onReject();
        }
    }

    function gasBackAddress() internal virtual view returns(address) {
        return initializer;
    }

    function close() external override virtual view {
        require(
            status() != Status.Pending || now > createdAt + FORCE_CLOSE_TIMEOUT,
            ErrorCodes.WRONG_STATUS
        );

        address gasBackAddress_ = gasBackAddress();

        require(
            msg.sender == gasBackAddress_,
            ErrorCodes.SENDER_IS_NOT_EVENT_OWNER
        );

        transferAll(gasBackAddress_);
    }

    /*
        @dev Get event details
        @returns _initData Init data
        @returns _status Current event status
        @returns _confirmRelays List of relays who have confirmed event
        @returns _confirmRelays List of relays who have rejected event
        @returns _eventDataSignatures List of relay's TonEvent signatures
    */
    function getDetails() external view responsible returns (
        EverscaleEthereumEventInitData _eventInitData,
        Status _status,
        uint[] _confirms,
        uint[] _rejects,
        uint[] empty,
        bytes[] _signatures,
        uint128 balance,
        address _initializer,
        TvmCell _meta,
        uint32 _requiredVotes
    ) {
        _confirms = getVoters(Vote.Confirm);

        for (uint voter : _confirms) {
            _signatures.push(signatures[voter]);
        }

        return {value: 0, flag: MsgFlag.REMAINING_GAS, bounce: false} (
            eventInitData,
            status(),
            _confirms,
            getVoters(Vote.Reject),
            getVoters(Vote.Empty),
            _signatures,
            address(this).balance,
            initializer,
            meta,
            requiredVotes
        );
    }
}
