pragma ever-solidity >= 0.62.0;

pragma AbiHeader expire;

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "@broxus/contracts/contracts/access/InternalOwner.tsol";
import "@broxus/contracts/contracts/utils/RandomNonce.tsol";
import "@broxus/contracts/contracts/utils/CheckPubKey.tsol";

import "./../../../utils/TransferUtils.tsol";
import "./../../interfaces/proxy/INetworks.tsol";
import "./../../interfaces/alien-token-merge/merge-pool/IMergePool_V2.tsol";

import "ton-eth-bridge-token-contracts/contracts/interfaces/IAcceptTokensTransferCallback.tsol";
import "ton-eth-bridge-token-contracts/contracts/interfaces/IAcceptTokensMintCallback.tsol";
import "ton-eth-bridge-token-contracts/contracts/interfaces/IBurnableTokenWallet.tsol";
import "ton-eth-bridge-token-contracts/contracts/interfaces/ITokenWallet.tsol";
import "ton-eth-bridge-token-contracts/contracts/TokenWalletPlatform.tsol";
import "./../../../utils/ErrorCodes.tsol";
import "./../EventCreditGas.tsol";


/// @title Mediator
/// @notice Used as part of hidden bridge for sol/evm-ever-sol/evm token transfers
/// Difference from V1:
/// - Support multiple event closers and event deployers
contract Mediator_V2 is
    IAcceptTokensTransferCallback,
    IAcceptTokensMintCallback,
    INetworks,
    TransferUtils,
    InternalOwner,
    RandomNonce,
    CheckPubKey
{
    enum Operation {
        BurnToAlienProxy,
        BurnToMergePool,
        TransferToNativeProxy
    }

    TvmCell public alienTokenWalletPlatformCode;
    address public nativeProxy;
    address[] public eventClosers;
    address[] public eventDeployers;

    constructor(
        address _owner,
        address _nativeProxy,
        TvmCell _alienTokenWalletPlatformCode
    ) checkPubKey {
        require(_owner != address(0), ErrorCodes.ZERO_ADDRESS);
        require(_nativeProxy != address(0), ErrorCodes.ZERO_ADDRESS);
        tvm.accept();

        setOwnership(_owner);
        nativeProxy = _nativeProxy;
        alienTokenWalletPlatformCode = _alienTokenWalletPlatformCode;
    }

    function setNativeProxy(
        address _nativeProxy
    ) external onlyOwner cashBack {
        require(_nativeProxy != address(0), ErrorCodes.ZERO_ADDRESS);
        nativeProxy = _nativeProxy;
    }

    /// @notice Accept incoming mint
    function onAcceptTokensMint(
        address,
        uint128 amount,
        address remainingGasTo,
        TvmCell payloadWithNonce
    ) external override reserveAtLeastTargetBalance {
        _handleTokens(
            payloadWithNonce,
            msg.sender,
            amount,
            remainingGasTo
        );
    }

    /// @notice Accept incoming transfer
    function onAcceptTokensTransfer(
        address,
        uint128 amount,
        address sender,
        address,
        address remainingGasTo,
        TvmCell payloadWithNonce
    ) external override reserveAtLeastTargetBalance {
        require(sender == nativeProxy, ErrorCodes.NOT_NATIVE_PROXY);

        _handleTokens(
            payloadWithNonce,
            msg.sender,
            amount,
            remainingGasTo
        );
    }

    function _handleTokens(
        TvmCell payloadWithNonce,
        address wallet,
        uint128 amount,
        address remainingGasTo
    ) internal view {
        (uint32 nonce, TvmCell payload) = abi.decode(payloadWithNonce, (uint32, TvmCell));

        remainingGasTo = _includes(eventDeployers, remainingGasTo)
          ? _randomChoice(eventClosers)
          : remainingGasTo;

        (
            Operation operation,
            address proxy,
            TvmCell operationPayload
        ) = abi.decode(payload, (Operation, address, TvmCell));

        TvmCell withdrawPayload = _encodeWithdrawPayload(operation, operationPayload, nonce);

        if (operation == Operation.BurnToAlienProxy || operation == Operation.BurnToMergePool) {
            _burn(wallet, amount, proxy, remainingGasTo, withdrawPayload);
        } else if (operation == Operation.TransferToNativeProxy) {
            _transfer(wallet, amount, proxy, remainingGasTo, withdrawPayload);
        }
    }

    function _encodeWithdrawPayload(
        Operation operation,
        TvmCell payload,
        uint32 nonce
    ) internal pure returns(TvmCell withdrawPayload) {
        if (operation == Operation.BurnToAlienProxy) {
            (
                , // ignore nonce
                Network network,
                TvmCell burnPayload
            ) = abi.decode(payload, (uint32, Network, TvmCell));

            withdrawPayload = abi.encode(nonce, network, burnPayload);
        } else if (operation == Operation.BurnToMergePool) {
            (
                , // ignore nonce
                IMergePool_V2.BurnType burnType,
                address targetToken,
                TvmCell operationPayload
            ) = abi.decode(payload, (uint32, IMergePool_V2.BurnType, address, TvmCell));

            withdrawPayload = abi.encode(nonce, burnType, targetToken, operationPayload);
        } else if (operation == Operation.TransferToNativeProxy) {
            (
                , // ignore nonce
                Network network,
                TvmCell transferPayload
            ) = abi.decode(payload, (uint32, Network, TvmCell));

            withdrawPayload = abi.encode(nonce, network, transferPayload);
        } else {
            revert(ErrorCodes.UNKNOWN_MEDIATOR_OPERATION);
        }
    }

    function _transfer(
        address wallet,
        uint128 amount,
        address proxy,
        address remainingGasTo,
        TvmCell payload
    ) internal pure {
        ITokenWallet(wallet).transfer{
            bounce: false,
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED
        }(amount, proxy, EventCreditGas.getDeployTokenWalletValue(), remainingGasTo, true, payload);
    }

    function _burn(
        address wallet,
        uint128 amount,
        address proxy,
        address remainingGasTo,
        TvmCell payload
    ) internal pure {
        IBurnableTokenWallet(wallet).burn{
            bounce: false,
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED
        }(amount, remainingGasTo, proxy, payload);
    }

    function _deriveAlienTokenWallet(
        address root
    ) internal view returns(address) {
        TvmCell stateInit = tvm.buildStateInit({
            contr: TokenWalletPlatform,
            varInit: {
                root: root,
                owner: address(this)
            },
            pubkey: 0,
            code: alienTokenWalletPlatformCode
        });

        return address(tvm.hash(stateInit));
    }

    function setCreditBackend(
        address[] _eventClosers,
        address[] _eventDeployers
    ) external onlyOwner {
        require(!_includes(eventClosers, address(0)), ErrorCodes.ZERO_ADDRESS);
        require(!_includes(eventDeployers, address(0)), ErrorCodes.ZERO_ADDRESS);

        eventClosers = _eventClosers;
        eventDeployers = _eventDeployers;
    }

    function upgrade(
        TvmCell code
    ) external onlyOwner {
        TvmCell data = abi.encode(
            owner,
            nativeProxy,
            alienTokenWalletPlatformCode,
            _randomNonce,
            eventClosers,
            eventDeployers
        );

        tvm.setcode(code);
        tvm.setCurrentCode(code);

        onCodeUpgrade(data);
    }

    function onCodeUpgrade(
        TvmCell data
    ) private {
        tvm.resetStorage();

        address _eventCloser;
        address _eventDeployer;

        (
            owner,
            nativeProxy,
            alienTokenWalletPlatformCode,
            _randomNonce,
            _eventCloser,
            _eventDeployer
        ) = abi.decode(data, (address, address, TvmCell, uint, address, address));

        eventClosers = [_eventCloser];
        eventClosers = [_eventDeployer];
    }

    function _includes(
      address[] choices,
      address target
    ) private pure returns (bool) {
      for (address value: choices) {
        if (target == value) {
          return true;
        }
      }

      return false;
    }

    function _randomChoice(
      address[] choices
    ) private pure returns (address) {
      uint id = rnd.next(choices.length);

      return choices[id];
    }
}
